
! This APDL scripts simulates a straight interconnect line with equal thickness and width


*DEL,job
*DIM,job,STRING,80
job(1) = 'M_in_signal_nets'
/TITLE, 'M_in_signal_nets'
/FILNAME, 'M_in_signal_nets'

t_end=972000
t_ste=50

Run_ID = 1
current = 0.001
length =  300		! as a multiple of t_Cu
eff_r = 2.5		! influence of small feature sizes on resistivity
k_Cu = 399.0		! thermal conductivities
k_ILD = 0.2
t_Cu = 1e-07		! wire width and thickness
t_ILD = 1e-07
r = 0.9			! Self-Healing for AC lines
Z = 5			! effective charge number
Qt = 0.5		! heat of transport, eV


*DEL,time_array
*DIM,time_array,ARRAY,5
time_array(1) = 1, 0.01*t_end, 0.02*t_end, 0.08*t_end, t_end

/PREP7
ET,1,SOLID226,100111,,,,,1,1    ! structural-electric-thermal diffusion

SELTOL,1e-12

toff=273                ! TEMPERATURE OFFSET FROM ABSOLUTE ZERO, deg C
TOFFST,toff

MPTEMP,1,0,20,50,70,100,200							! Temperatures for material parameter input (°C)

MPDATA,RSVX,1,1, eff_r*1.54E-08, eff_r*1.69E-08, eff_r*1.91E-08, eff_r*2.05E-08, eff_r*2.27E-08, eff_r*3E-08 	! temperature-dependent RESISTIVITY rho, Ohm-m -> Cu 


MP,DXX,1,7.8e-5		! DIFFUSIVITY, m^2/s
MP,BETX,1,1             ! coef. of diffusion expansion for back stress
MP,CREF,1,1
MP,REFT,1,100		! Refrence Temperature
MP,EX,1,129.8e9         ! Young's modulus B, Pa
MP,PRXY,1,0.34          ! Poisson's ratio
!MP,RSVX,1,eff_r*2.27E-08      ! RESISTIVITY rho, Ohm-m -> Cu @ 100°C (for constant resistivity without temperature dependency)

MP,KXX,1,k_Cu		! Thermal Conductivity Cu
MP,C,1,382		! Specific heat (capacity?) J/kg*K
!MP,ALPX,1,10e-6	!coefficient of thermal expansion

Z_EM=(1-r)*Z		! Z decreased by self-healing
Ze=-Z_EM*1.6e-19        ! (POSITIVE?) effective charge, C  -> Z*e
kB=1.3806e-23           ! Boltzmann constant, m^2*kg/(s^2*degK)
Va=1.18e-29             ! atomic volume, m^3 
k_eV=8.61733e-5		! Boltzmann's constant, eV/K

Ea=0.6			! Activation Energy, eV		

! Migration Atoms
TB,MIGR,1
TBDATA,1,Ea/k_eV	
TBDATA,2,Va/kB
tbdata,3,Qt/k_eV
TBDATA,4,Ze/kB



! 3D - Block
l=t_Cu*length                ! LENGTH, m
w=h
BLOCK,0,l,0,w,0,h

! MESH 
VSEL,ALL 
ESIZE,h/2
VSEL,ALL
MSHKEY,0
MSHAPE,1,3D
VMESH,ALL


/VIEW,1,1,1,1
/VUP,1,Z
/ANGLE,1,180,ZM,0
EPLOT
/IMAGE,SAVE,'mesh_TM',png
ALLSEL

! Ground
NSEL,S,LOC,X,0,0
D,ALL,VOLT,0
F,NDNEXT(0),AMPS,-current

ALLSEL,ALL
! Current
NSEL,S,LOC,X,l
CP,NEXT,VOLT,ALL
F,NDNEXT(0),AMPS,current



ALLSEL,ALL
! Temperatur
t=100                  ! TEMPERATURE, °C
ALLSEL,ALL
NSEL,S,LOC,X,0,0
D,ALL,TEMP,t		! constant temperature at via
NSEL,S,LOC,X,l
D,ALL,TEMP,100

ALLSEL,ALL
ASEL,ALL,EXT 
ASEL,U,LOC,X,0
ASEL,U,LOC,X,l
SFA,ALL,,CONV,k_ILD/t_ILD,100		! convection to dielectric
!SFA,ALL,,CONV,1e20,100 		! use for TM deactivation



! Displacement
ALLSEL
D,ALL,UX,0
D,ALL,UY,0
D,ALL,UZ,0
FINISH

/SOLU
ANTYPE,TRANS
ALLSEL,ALL
IC,ALL,CONC,1
NSEL,ALL

NLGEOM,ON
OUTRES,ALL,ALL
AUTOTS,OFF

KEYW,pr_sgui,1  ! Disables Solution is done
TIME,t_end
NSUBST,t_ste
SOLVE
FINISH

!Number format
/GFORMAT,F,12,2
! White Background
/RGB,INDEX,100,100,100, 0
/RGB,INDEX, 80, 80, 80,13
/RGB,INDEX, 60, 60, 60,14
/RGB,INDEX, 0, 0, 0,15
!export all parameters to file.param
PARSAV,ALL,file,parm
!To smooth contour, make sure to start ansys in win32c
/CONTOUR,ALL,128
!Should display location number, but remove element border
/PNUM,LOC,1
! min max in plot
/PLOPTS,MINM,0
! x,y,z axis
/TRIAD,RBOT

FINISH

! Output of results

/POST1
/AXLAB,X,x (m)
/AXLAB,Y, Hydrostatic stress (Pa)
PATH,x_axis,2,,100
PPATH,1,,0,0,0
PPATH,2,,l,0,0
/XRANGE,0,l
/YRANGE,-400e6,400e6
/GROPT,DIVY,4

*DO, ti, 1, 5, 1
    SET,,,,,time_array(ti)
    color=ti*2+2
    /COLOR,CURVE,color
    PDEF,time_array(ti),NL,HPRES ,AVG
    PLPATH,time_array(ti)

    PAGET,datash,TABLE
    ! x, y, z, s, sh -> s...length, sh...stress_hydro
    *MWRITE,datash,'sh_x_axis_%job(1)%_t%ti%_%time_array(ti)%_x_y_z_s_sh',txt
    %15.10F %15.10F %15.10F %15.10F %15.2F

    PDEF,time_array(ti),TEMP
    PLPATH,time_array(ti)

    PAGET,datatemp,TABLE
    ! x, y, z, s, sh -> s...length, sh...stress_hydro
    *MWRITE,datatemp,'temp_x_axis_%job(1)%_t%ti%_%time_array(ti)%_x_y_z_s_sh',txt
    %15.10F %15.10F %15.10F %15.10F %15.2F

    PDEF,time_array(ti),VOLT
    PLPATH,time_array(ti)

    PAGET,datav,TABLE
    ! x, y, z, s, sh -> s...length, sh...stress_hydro
    *MWRITE,datav,'v_x_axis_%job(1)%_t%ti%_%time_array(ti)%_x_y_z_s_sh',txt
    %15.10F %15.10F %15.10F %15.10F %15.10F


    /NOERASE
*ENDDO

!/CLEAR,NOSTART

/EOF